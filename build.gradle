buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "$minecraft_version-$mod_version"

if (System.getenv().DRONE_BUILD_NUMBER != null) {
	version += "-snapshot-" + System.getenv().DRONE_BUILD_NUMBER
}

minecraft {
	version = "$minecraft_version-$forge_version"
	runDir = "run"

	replaceIn "zombierain/ZombieRain.java"
	replace '@VERSION@', "$mod_version"
}

repositories {
	maven {
		name = "shadowfacts"
		url = "http://maven.shadowfacts.net"
	}
}

dependencies {
	compile group: "net.shadowfacts", name: "ShadowAPI", version: "1.7.10-2.0.2"
}

processResources
{
	inputs.property "version", "$mod_version"

	from (sourceSets.main.resources.srcDirs) {
		include '**/*.info'
		expand 'version': "$mod_version", 'mcversion': "$minecraft_version"
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
	}
}

configurations {
	includedLibs
}

dependencies {
	includedLibs group: "net.shadowfacts", name: "ShadowAPI", version: "1.7.10-2.0.2"
}

task fatJar(type: Jar) {
	classifier = 'fat'
	from {configurations.includedLibs.collect {it.isDirectory() ? it : zipTree(it)}}
	with jar
}

task deobfJar(type: Jar) {
	classifier = 'deobf'
	from sourceSets.main.output
	from {configurations.includedLibs.collect {it.isDirectory() ? it : zipTree(it)}}
}

tasks.build.dependsOn fatJar, deobfJar

artifacts {
	archives jar
	archives fatJar
	archives deobfJar
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: "file:///maven/")
	}
}

// IDEA bug
idea { module { inheritOutputDirs = true } }